buildscript {
	ext {
		springBootVersion = '2.2.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'com.devsoap.vaadin-flow' version '1.2'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply from: 'liquibase.gradle'

group = 'com.jeno'
version = '1.0.0'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
	mavenCentral()
}

ext {
	vaadinVersion = '14.1.2'
}

configurations {
	runtime.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
	compile.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

vaadin.autoconfigure()

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')

	compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: '2.0.0.RELEASE'

	compile('com.vaadin:vaadin-spring-boot-starter')

	compile("org.liquibase:liquibase-core:3.8.2") {
		exclude group: "org.slf4j", module: "slf4j-api"
		exclude group: "ch.qos.logback", module: "logback-classic"
	}

	compile('it.ozimov:spring-boot-email-core:0.6.3')
	compile('com.google.guava:guava:23.0')
	compile('io.reactivex.rxjava2:rxjava:2.1.10')
	compile('org.apache.commons:commons-csv:1.5')

	runtime('mysql:mysql-connector-java')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

task stage(type: Copy, dependsOn: [clean, build]) {
	from jar.archivePath
	into project.rootDir
	rename {
		'app.jar'
	}
}
stage.mustRunAfter(clean)

clean.doLast {
	project.file('app.jar').delete()
}